% Original file:
% https://bitbucket.org/lpsmasters/lps_corner/src/06b0de6227f751547e8d25937c05fa30208e45f5/examples/goat.lps?at=master

maxTime(8).
cycleInterval(500).

actions([
  row(_, _),
  transport(_, _, _)
]).
fluent(loc(_, _)).

initially([
  loc(wolf, south),
  loc(goat, south),
  loc(cabbage, south),
  loc(farmer, south)
]).

loc(Object, south, T1), Object != farmer ->
  makeLoc(Object, north, T2, T3).

makeLoc(Object, Location1, T1, T3) <-
  Object != farmer,
  loc(Object, Location2, T1),
  Location1 != Location2,
  makeLoc(farmer, Location2, T1, T2),
  row(Location2, Location1, T2, T3),
  transport(Object, Location2, Location1, T2, T3),
  dealWithGoat(Location2, Location1, T2, T3).

makeLoc(farmer, Location1, T1, T2) <-
  loc(farmer, Location2, T1),
  Location1 != Location2,
  row(Location2, Location1, T1, T2),
  dealWithGoat(Location2, Location1, T1, T2).

dealWithGoat(Location2, Location1, T1, T2) <-
  not loc(goat, Location2, T1).

dealWithGoat(Location2, Location1, T1, T2) <-
  not loc(wolf, Location2, T1),
  not loc(cabbage, Location2, T1).

dealWithGoat(Location2, Location1, T1, T2) <-
  loc(goat, Location2, T1),
  loc(wolf, Location2, T1),
  transport(goat, Location2, Location1, T1, T2).

dealWithGoat(Location2, Location1, T1, T2) <-
  loc(goat, Location2, T1),
  loc(wolf, Location2, T1),
  not loc(cabbage, Location2, T1),
  transport(wolf, Location2, Location1, T1, T2).

dealWithGoat(Location2, Location1, T1, T2) <-
  loc(goat, Location2, T1),
  loc(cabbage, Location2, T1),
  transport(goat, Location2, Location1, T1, T2).

dealWithGoat(Location2, Location1, T1, T2) <-
  loc(goat, Location2, T1),
  loc(cabbage, Location2, T1),
  not loc(wolf, Location2, T1),
  transport(cabbage, Location2, Location1, T1, T2).

makeLoc(Object, Location, T, T) <-
  loc(Object, Location, T).

updates(transport(O, L1, L2), loc(O, L1), loc(O, L2)).
updates(row(L1, L2), loc(farmer, L1), loc(farmer, L2)).

<- transport(O1, L1, L2), transport(O2, L1, L2), O1 != O2.
<- row(south, north), row(north, south).
